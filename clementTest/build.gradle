//标识是library库
apply plugin: 'com.android.library'

//获取git的提交次数，将提交次数作为versionCode
static int gitVersionCode() {
    def count = "git rev-list HEAD --count".execute().text.trim()
    return count.isInteger() ? count.toInteger() : 0
}

//library info
def LIB_GROUP = "com.clement.libraryTest"
def LIB_NAME = "clementTest"
def LIB_VERSION_NAME = "0.0.6"

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode gitVersionCode()
        versionName LIB_VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api 'com.google.code.gson:gson:2.8.5'
}

//上传aar到maven
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//项目源码的github地址
def siteUrl = 'https://github.com/Developmc/MavenLibraryTest'    // project homepage
def gitUrl = 'https://github.com/Developmc/MavenLibraryTest.git' // project git

//定义引用时的group
group = LIB_GROUP
//版本号
version = LIB_VERSION_NAME
//上面配置后上传至JCenter后的编译路径是这样的： implementation 'group:artifactId:version'

//传到jcenter至少需要四个文件，除了打包的aar之外，还需要pom和javadoc，source，否则是通不过jcenter的审核

//打包sourcesJar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//打包javadoc
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//解决 JavaDoc 中文注释生成失败的问题
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

//定义pom并打包aar
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            //Set your license
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            dependencies {
                dependency {
                    groupId 'com.google.code.gson'
                    artifactId 'gson'
                    version '2.8.5'
                }
            }

        }
    }
}

//配置bintray参数,bintrayUser和bintrayApiKey的值保存在local.properties内，可通过登录 https://bintray.com/developmc 获取
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {

        repo = "maven"               //跟创建的Maven仓库名字保持一致
        name = LIB_NAME                //发布到JCenter上的项目名字
        desc = "Library for test"      //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
